plugins {
    id 'java-library'
    id 'com.github.ben-manes.versions' version '0.53.0'
    id("com.diffplug.spotless") version "8.2.1"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencyUpdates {
    rejectVersionIf {
        ['alpha', 'beta', 'rc', 'm', 'snapshot', 'preview', 'eap', 'dev'].any { keyword ->
            it.candidate.version.toLowerCase().contains(keyword)
        }
    }
    // Опционально: проверка только стабильных версий
    checkForGradleUpdate = true
    outputFormatter = 'json'
}

def junitVersion = '6.0.3'

dependencies {
    testImplementation(
            platform("org.junit:junit-bom:$junitVersion"),
            'org.junit.jupiter:junit-jupiter',
            'org.slf4j:slf4j-simple:2.0.17',
            'com.codeborne:selenide:7.14.0',
            'net.datafaker:datafaker:2.5.4'
    )
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}


tasks.withType(Test).configureEach {
    systemProperties(System.getProperties())
    useJUnitPlatform()
    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat = "short"
        }
    }
}

tasks.named('dependencyUpdates') {
    doFirst {
        gradle.startParameter.setParallelProjectExecutionEnabled(false)
    }
}

spotless {
    // optional: limit format enforcement to just the files changed by this feature branch
    ratchetFrom 'origin/main'

    format 'misc', {
        // define the files to apply `misc` to
        target '*.gradle', '.gitattributes', '.gitignore'

        // define the steps to apply to those files
        trimTrailingWhitespace()
        leadingTabsToSpaces()
        endWithNewline()
    }
    java {
        encoding 'UTF-8'
        googleJavaFormat('1.34.1').aosp().reflowLongStrings().skipJavadocFormatting()
        formatAnnotations()
    }
}
